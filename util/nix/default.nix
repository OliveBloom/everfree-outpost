
with builtins;

let pkgs = import ./pinned-nixpkgs.nix {};
    inherit (pkgs) stdenv lib fetchurl fetchzip callPackage buildEnv;

    pinnedRust = import ./pinned-rust.nix pkgs {
        minorVersion = 16;
        srcHash = "1d78jq7mc34n265by68amr9r4nzbiqrilfbwh7gx56ydn4gb6rpr";
        bootstrapHashes = {
            # Generated by running `nixpkgs/.../rust/print-hashes.sh 1.15.0`
            i686-unknown-linux-gnu = "3cff0c621a9c58dfaf29d2d08655030ea90cfbd009e71a04e073d67e5177b372";
            x86_64-unknown-linux-gnu = "576fcced49744af5ea438afc4411395530426b0a3d4839c5205f646f15850663";
            aarch64-unknown-linux-gnu = "995b475b798cde9456ad3036630af859d7dbc1da3013e088a9b78be6efd0d76f";
            i686-apple-darwin = "92c7e87b774eb834031c2105053e615f54f5ca6e03d310513dd7c71fcac16c15";
            x86_64-apple-darwin = "8b02c3714d30a6111af805d76df0de28c045f883a9171839ebd5667327f2e50a";
        };
    };
    inherit (pinnedRust) rustc rustSrc;


    inherit (import ./rust-libs.nix (pkgs // { inherit rustc; }))
        rustDep buildRustLibDir;

    rustDepDrvs = rec {
        bitflags = rustDep "bitflags-0.5.0" {
            sha256 = "0bgw1kiy121kikjrwj6zsd7l8n1gg1jirivzkc7zpjsvqa3p0hla";
        };
        libc = rustDep "libc-0.2.8" {
            sha256 = "103i6v93lm1jc8r2mkj5y9p62ss9ygr05zdr81dyh11nph8syh7n";
        };
        rand = rustDep "rand-0.3.14" {
            sha256 = "0bjzb2ngjkgc1kmc7gzw2b8imv2d3fkg0x2qd5qw0bl9f7h6km2p";
            dependencies = [ libc ];
        };
        rustc-serialize = rustDep "rustc-serialize-0.3.18" {
            sha256 = "1336vx95b0f5xck691hhbylgjwlaa7lwx21x63rkrms9pfypabsh";
        };
        time = rustDep "time-0.1.34" {
            sha256 = "08qxwig2w7wh9p9pf35ygzs2ny9xdv4x3qhasxrcyz6xf3iahsr7";
            dependencies = [ libc ];
        };

        linked-hash-map = rustDep "linked-hash-map-0.0.9" {
            sha256 = "0cg5p6m9ka9isg370p5sszs81h3gg8wvy9xv86fk6nc45jiqdzaf";
        };

        python3-sys = lib.overrideDerivation
            (rustDep "python3-sys-0.1.3" {
                sha256 = "01z3dbqwfraxgkwi83n6ckxidgaw1fcrgbpmwmva3fsyx8jm0niw";
                dependencies = [ libc ];
                buildDependencies = [ regex ];
                buildInputs = [ pkgs.python3 ];
            })
            # build.rs relies on CARGO_FEATURE_XYZ env vars, which
            # buildRustCrate does not yet support
            (_: {
                CARGO_FEATURE_PYTHON_3 = "1";
                CARGO_FEATURE_PEP_384 = "1";
            });

        log = rustDep "log-0.3.5" {
            sha256 = "00279rcly8c6w7w4y92gsxl146m2rxmh1k9s8hz14y7gbi1j0ig4";
            features = [ "use_std" ];
            dependencies = [ libc ];
        };
        env_logger = rustDep "env_logger-0.3.2" {
            sha256 = "03l06frgpbl990z20a91lp9w9vir6ikgmi6pxh0zn1havsy7waqk";
            dependencies = [ log regex ];
        };

        regex = rustDep "regex-0.1.55" {
            sha256 = "00lb300gvm5w0353pvkmic17h2g8gw0i25q2adyy6h2ad25i8imq";
            dependencies = [ aho-corasick memchr regex-syntax utf8-ranges ];
        };
        regex-syntax = rustDep "regex-syntax-0.2.5" {
            sha256 = "0b1al9ilxv7ldnbbax0alkdmcjh0h5xn3md4sy2s9wxxdxs27kbd";
        };
        aho-corasick = rustDep "aho-corasick-0.5.1" {
            sha256 = "1bg4asdxilfbsawm9fj6946dlidsi0jqr7i52vz78q6i80n397dz";
            dependencies = [ memchr ];
        };
        memchr = rustDep "memchr-0.1.10" {
            sha256 = "0whcrwnmqn5w4zb6sq2kpabyx93k8q8bn963rf4v5c07fsx7qj8j";
            dependencies = [ libc ];
        };
        utf8-ranges = rustDep "utf8-ranges-0.1.3" {
            sha256 = "1cj548a91a93j8375p78qikaiam548xh84cb0ck8y119adbmsvbp";
        };

        num = rustDep "num-0.1.36" {
            sha256 = "1p1vyczf75a39vxcid8d5w1jmjybd0akgn3s3vmrjslhv9sfvqb3";
            dependencies = [ num-integer num-iter num-traits ];
        };
        num-integer = rustDep "num-integer-0.1.32" {
            sha256 = "0l52mizdca5967vnsscwahwg27aklih52cznmvlrx7sina87jcg0";
            dependencies = [ num-traits ];
        };
        num-iter = rustDep "num-iter-0.1.32" {
            sha256 = "14a4710b037jih43fxav1ahzh2k419vsz9ym5pkrcciynczr4niv";
            dependencies = [ num-traits num-integer ];
        };
        num-traits = rustDep "num-traits-0.1.36" {
            sha256 = "1wzf0jd77dbagjqk9ygd6dy1729zg7gj7flf3sa54n7qvfhcwi79";
        };
        num-rational = rustDep "num-rational-0.1.36" {
            sha256 = "0jibhs8xiap2wlv1xjwdvhyj4yrxwfisqbnfm53vjm5ldlijp87p";
            dependencies = [ num-traits num-integer ];
        };

        sdl2 = rustDep "sdl2-0.29.0" {
            sha256 = "1f3va8g5ng5xv2mnbdi3ayg3lg08yidrwgqhy2j47xjishg8bihk";
            libPath = "src/sdl2/lib.rs";
            dependencies = [ bitflags lazy_static libc num rand sdl2-sys ];
        };
        sdl2-sys = rustDep "sdl2-sys-0.27.2" {
            sha256 = "0igj45rf6zik21rasl1fsy1j7x798kxq1lvlqc9szky7bx1p60kl";
            dependencies = [ libc ];
        };
        lazy_static = rustDep "lazy_static-0.2.2" {
            sha256 = "006gi7k0a5s67hwd6csfqddzq7pxwwgasyxch0hdhghjk9bj11yz";
        };

        gl_generator = rustDep "gl_generator-0.5.2" {
            sha256 = "1cdk2nv9g9p1p4837m9qjpqb5k7k52yvvzjmvfb90phxgzgdfk48";
            libPath = "lib.rs";
            dependencies = [ xml khronos_api log ];
        };
        xml = rustDep "xml-rs-0.3.6" {
            libName = "xml";
            sha256 = "1g1cclib7fj900m4669vxlz45lxcq0m36g7cd8chl494c2xsgj15";
            dependencies = [ bitflags ];
        };
        khronos_api = rustDep "khronos_api-1.0.0" {
            sha256 = "0rx0ck2kn3xp1d17v4jwdqyb1bf33i7m1gvablhnzyg9mx3g1ycm";
        };

        png = rustDep "png-0.6.2" {
            sha256 = "03i78w5jbvk9y6babfrh7h0akvg81pcyyhniilv24z5v0vh5jvjs";
            dependencies = [ inflate deflate num-iter bitflags ];
            features = [ "png-encoding" ];
        };
        inflate = rustDep "inflate-0.1.1" {
            sha256 = "112kh9hjcjjxdybl032mdhpwnr3qxw8j0ch6hwanwpcf3gz42g1h";
        };
        deflate = rustDep "deflate-0.7.17" {
            sha256 = "022jfc5rwhd9xgsgap8983ihnginmymcsh733g37dmsq2k6yq10x";
            dependencies = [ byteorder adler32 ];
        };
        adler32 = rustDep "adler32-1.0.2" {
            sha256 = "1974q3nysai026zhz24df506cxwi09jdzqksll4h7ibpb5n9g1d4";
        };

        image = rustDep "image-0.12.3" {
            sha256 = "12xdzi29vr19gz3h93c1ihyvyv9xar9sp0inrjwwvlbjvn8nn0p9";
            dependencies = [ byteorder enum_primitive
                num-iter num-rational num-traits
                png
            ];
            features = [ "png_codec" "png" ];
        };
        byteorder = rustDep "byteorder-1.2.1" {
            sha256 = "1wsxnqcscg4gchdmgdbwc78lw2qx2i6bnjd564xq7h7qc4fp2157";
            features = [ "std" ];
        };
        enum_primitive = rustDep "enum_primitive-0.1.1" {
            sha256 = "1a225rlsz7sz3nn14dar71kp2f9v08s3rwl6j55xp51mv01f695y";
            dependencies = [ num-traits ];
        };
    };

    rustDeps = buildRustLibDir (attrValues rustDepDrvs);

    bitflagsSrc = rustDepDrvs.bitflags.src;


    emscriptenFastcomp = import ./pinned-emscripten-fastcomp.nix pkgs {
        rev = "1.37.0";
        sha256 = "0bpjz86gii9njk885rw804i308697my96r6ajpcs2ic4sri9wsix";
    };


    # The version of imagemagick currently in nixpkgs is 6.9.9-26, which
    # crashes when loading .xcf files through python3-wand.  Fixed in 6.9.9-28.
    pinnedImagemagick = import ./pinned-imagemagick.nix pkgs {
        version = "6.9.9-28";
        sha256 = "132kdl7jlkghfg3smdab14c29cpvrx65ibipxkmwg1nc88ycqivh";
    };
    inherit (pinnedImagemagick) Wand;


    elfLoader = import ./elf-loader.nix pkgs;


in pkgs.mkShell {
    buildInputs = attrValues {
        inherit rustc emscriptenFastcomp Wand;
        inherit (pkgs)
            stdenv ninja
            python3 closurecompiler optipng
            SDL2 boost websocketpp ncurses
            patchelf;
        glibc_out = pkgs.glibc.out;
        glibc_static = pkgs.glibc.static;
        inherit (pkgs.haskellPackages) pandoc;
        inherit (pkgs.python3Packages) pillow pyyaml;

        # Run-time dependencies
        inherit (pkgs.python3Packages) bcrypt tornado pynacl flask requests;
    };
    inherit rustDeps rustSrc bitflagsSrc;

    OUTPOST_CONFIGURE_ARGS =
        "--rust-extra-libdir=${rustDeps} " +
        "--rust-home=${rustSrc} " +
        "--bitflags-home=${bitflagsSrc} " +
        (if elfLoader == null then "" else
            "--nix-patch-elf-loader=" + elfLoader + " ");
}
